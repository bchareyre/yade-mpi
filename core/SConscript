# syntax: python
Import('*')

pyMain='$PREFIX/bin/yade$SUFFIX'
main=env.ScanReplace('main/main.py.in')
multi=env.ScanReplace('main/yade-multi.in')
env.AlwaysBuild(main)
env.AlwaysBuild(multi)
env.InstallAs(pyMain,main)
env.InstallAs(pyMain+'-multi',multi)
env.AddPostAction(pyMain,Chmod(pyMain,0755))
env.AddPostAction(pyMain+'-multi',Chmod(pyMain+'-multi',0755))

env.Install('$PREFIX/lib/yade$SUFFIX/py/yade',[
	env.SharedLibrary('boot',['main/pyboot.cpp'],SHLIBPREFIX='',LIBS=env['LIBS']+['yade-support','core'])
])

# env.InstallAs('$PREFIX/bin/yade$SUFFIX-cxx',env.Program('yade',['main/main.cpp'],LIBS=env['LIBS']+['yade-support','core'],CPPDEFINES=env['CPPDEFINES']+[('YADE_VERSION','\\"'+env['version']+'\\"'),]))
## remove old executable
## might be removed in the future
import os.path, os
cxxExec=env.subst('$PREFIX/bin/yade$SUFFIX-cxx')
if os.path.exists(cxxExec):
	print "Deleting old executable",cxxExec
	os.remove(cxxExec)

env.Install('$PREFIX/lib/yade$SUFFIX/lib',[
	env.SharedLibrary('core',
		env.Combine('core.cpp',[
			'Body.cpp',
			'BodyContainer.cpp',
			'Bound.cpp',
			'Cell.cpp',
			'Collider.cpp',
			'PartialEngine.cpp',
			'Engine.cpp',
			'FileGenerator.cpp',
			'FrontEnd.cpp',
			'Interaction.cpp',
			'InteractionContainer.cpp',
			'GroupRelationData.cpp',
			'Material.cpp',
			'Scene.cpp',
			'Dispatcher.cpp',
			'Omega.cpp',
			'Shape.cpp',
			'SimulationFlow.cpp',
			'State.cpp',
			'ThreadRunner.cpp',
			'ThreadWorker.cpp',
			'TimeStepper.cpp',
			'corePlugins.cpp'			
			]
			),
		LIBS=env['LIBS']+[
			'yade-support',
			'rt', # realtime lib, for clock_gettime 
			]+
			(['$QGLVIEWER_LIB','yade-opengl'] if 'YADE_OPENGL' in env['CPPDEFINES'] else [])
			+(['miniWm3'] if 'wm3' in env['features'] else [])
			,
	)
])
